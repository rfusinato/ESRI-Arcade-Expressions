function getEndPointFieldValue(pointLayerName, bufferDist, getFirst, fieldName) {
    // Get point layer from the map
    var pointLayer = FeatureSetByName($map, pointLayerName, [fieldName], true)

    // Get geometry and paths of the line feature
    var geom = Geometry($feature)
    var paths = geom.paths

    // Return null if paths are empty
    if (Count(paths) == 0) {
        return null
    }

    // Pick first or last path point
    var path = paths[-1]
    var endPoint = Point(path[-1])
    if(getFirst){
      path = paths[0]
      endPoint = Point(path[0])
    }

    // Create buffer for intersection
    var endPointBuffer = Buffer(endPoint, bufferDist)

    // Get intersecting points
    var intersect = Intersects(pointLayer, endPointBuffer)

    // Return field value or null if none found
    if(Count(intersect) > 0){
      return First(intersect)[fieldName]
    }
    else {
      return null
    }
}

// Example usage:
// Get 'facility_id' at the first point of the line


var value = getEndPointFieldValue("Manhole Inspection Report", 0.5, true, "mh_structure_no")
if(IsEmpty(value)) {
  value = getEndPointFieldValue("Structures", 0.5, true, "facility_id")
}

return value
